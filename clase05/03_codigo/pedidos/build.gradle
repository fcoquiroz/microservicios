buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.15'
  }
}

plugins {
  id 'org.asciidoctor.convert' version '1.5.3'
  id 'org.springframework.boot' version '2.1.3.RELEASE'
  id 'com.bmuschko.docker-remote-api' version '4.6.2'
  id 'java'
  id 'eclipse'
  id 'idea'
  id 'jacoco'
}

apply plugin: 'io.spring.dependency-management'

sourceCompatibility = '1.8'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

ext {
  set('springBootAdminVersion', '2.1.3')
  set('springCloudVersion', 'Greenwich.SR1')
  set('snippetsDir', file('build/generated-snippets'))
}

bootJar {
   archiveName = 'service.jar'
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'de.codecentric:spring-boot-admin-starter-client'
  implementation 'org.flywaydb:flyway-core'
  implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
  implementation 'org.springframework.cloud:spring-cloud-starter-netflix-hystrix'
  implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
  implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
  implementation 'org.springframework.cloud:spring-cloud-starter-zipkin'

  implementation 'org.jadira.usertype:usertype.spi:7.0.0.CR1'
  implementation 'org.jadira.usertype:usertype.core:7.0.0.CR1'
  implementation 'org.codehaus.groovy:groovy-all:2.4.8'

  implementation 'io.dropwizard.metrics:metrics-core'
  implementation 'io.micrometer:micrometer-registry-prometheus'

  runtimeOnly 'org.jolokia:jolokia-core'

  compileOnly 'org.projectlombok:lombok'
  testCompileOnly 'org.projectlombok:lombok'
  runtimeOnly 'org.postgresql:postgresql'
  annotationProcessor 'org.projectlombok:lombok'
  testAnnotationProcessor 'org.projectlombok:lombok'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

dependencyManagement {
  imports {
    mavenBom "de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}"
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

test {
  outputs.dir snippetsDir
}

asciidoctor {
  inputs.dir snippetsDir
  dependsOn test

  sourceDir 'src/main/asciidoc'
  backends 'pdf', 'html'
  attributes  \
       'snippets': file('build/snippets'),
        'toc': 'left'
}


task createDockerfile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile) {
  destFile = project.file('build/libs/Dockerfile')
  from 'openjdk:8u181-jre-slim-stretch'
  copyFile 'service.jar', '/opt/service.jar'
  exposePort 8080
  entryPoint 'java', "-Djava.awt.headless=true", "-Xms128m", "-Xmx128m", '-jar', '/opt/service.jar'
}

task buildImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
  dependsOn createDockerfile
  inputDir = createDockerfile.destFile.get().asFile.parentFile
    tags.add('circulo7/curso-ms-pedidos-service:' + project.version.toLowerCase())
}

task pushImage(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
  dependsOn buildImage
  imageName = buildImage.tags.get().first()
}

docker {
  registryCredentials {
    url = (System.getenv('CONTAINER_REGISTRY_URL') ?: project.properties.getOrDefault('containerRegistryUrl', 'https://registry.hub.docker.io'))
    username = (System.getenv('CONTAINER_BUILDER_USERNAME') ?: (project.hasProperty('containerRegistryUsername') ? containerRegistryUsername : '')) ?: ''
    password = (System.getenv('CONTAINER_BUILDER_PASSWORD') ?: (project.hasProperty('containerRegistryPassword') ? containerRegistryPassword : '')) ?: ''
  }
}

createDockerfile.dependsOn bootJar


